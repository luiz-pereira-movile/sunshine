apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'com.github.kt3k.coveralls'

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        applicationId "com.movile.sunshine"
        minSdkVersion 21
        targetSdkVersion 22
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            debuggable true
            testCoverageEnabled true
        }
    }
    lintOptions {
        abortOnError false // ignoring some references from dagger-compiler
    }
}

apply plugin: 'jacoco'
jacoco {
    toolVersion = "0.7.5.201505241946"
}

coveralls {
    jacocoReportPath = "${buildDir}/reports/jacoco/jacocoRootReport/jacocoRootReport.xml"
}

task jacocoTestReport(type: JacocoReport, dependsOn: ['testDebug']) {
    description = "Generates Jacoco coverage reports: XML and HTML"
    group = "Reporting"

    sourceDirectories = files(["src/main/java", "src/test/java"])

    classDirectories = fileTree(
            dir: 'build/intermediates/classes',
            excludes: ['**/R.class',
                       '**/R$*.class',
                       '**/BuildConfig.*',
                       '**/Manifest*.*',
                       '**/*Activity*.*',
                       '**/*Fragment*.*'
            ]
    )
    executionData = fileTree(dir: 'build/jacoco', includes: ["*.exec"])

    reports {
        xml.enabled = true
        csv.enabled false
        html.enabled = true
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:22.2.0'

    compile 'javax.inject:javax.inject:1'
    compile 'javax.annotation:javax.annotation-api:1.2'
    compile 'com.google.dagger:dagger:2.0'

    compile 'com.google.code.gson:gson:2.3.1'
    compile 'com.squareup.retrofit:retrofit:1.9.0'

    compile 'com.jakewharton:butterknife:7.0.1'

    apt 'com.google.dagger:dagger-compiler:2.0'
    provided 'org.glassfish:javax.annotation:10.0-b28'

    testCompile 'junit:junit:4.+'
    testCompile "org.mockito:mockito-core:2.+"
}
